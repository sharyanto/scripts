#!/usr/bin/env perl

# feed it lines containing URLs in each, and it will extract search query logs

use URI::Escape;
use strict;

sub unesc {
  local $_ = shift;
  $_ = uri_unescape($_);
  s/\+/ /g;
  $_;
}

while (<>) {
  my ($url) = m#(https?://.+)#i or next;
  my ($host, $rest) = $url =~ m!https?://([^/]+)(.*)!i or next;
  my ($site, $query);
  my $x;

  if ($host =~ /google/i) {
    $rest =~ m!/(images|search|videosearch|news|maps|blogsearch|books|groups)!i or next;
    $x = $1;
    $site =
        $x =~ /images/ ? "google images" :
        $x =~ /videosearch/ ? "google video" :
        $x =~ /maps/ ? "google maps" :
        $x =~ /news/ ? "google news" :
        $x =~ /blogsearch/ ? "google blogs" :
        $x =~ /groups/ ? "google groups" :
        "google";
    $rest =~ /q=(.+?)(?:&|\z)/ or next;
    $query = unesc($1);
  } elsif ($host =~ /isohunt\.com/i) {
    $site = "isohunt";
    $rest =~ m!/torrents\.php! or next;
    $rest =~ /ihq=(.+?)(?:&|\z)/ or next;
    $query = unesc($1);
  } elsif ($host =~ /mininova\.org/i) {
    $site = "mininova";
    $rest =~ m!/search/([^/]+)/! or next;
    $query = unesc($1);
  } elsif ($host =~ /kat\.ph/i) {
    $site = "kat";
    $rest =~ m!/search/([^/]+)/! or next;
    $query = unesc($1);
  } elsif ($host =~ /filedonkey\.com/i) {
    $site = "filedonkey";
    $rest =~ m!/search\.html! or next;
    $rest =~ /order=(.+?)(?:&|\z)/ or next;
    $query = unesc($1);
  } elsif ($host =~ /filehash\.com/i) {
    $site = "filehash";
    $rest =~ m!/search/([^/]+)/! or next;
    $query = unesc($1);
  } elsif ($host =~ /smartorrent/i) {
    $site = "smartorrent";
    $rest =~ m!term=(.+?)(?:&|\z)! or next;
    $query = unesc($1);
  # 2008-10-16 - sekarang gw mulai sering pake btjunkie & TPB, abis isohunt gak lengkap sih
  } elsif ($host =~ /thepiratebay/i) {
    $site = "thepiratebay";
    $rest =~ m!/search/(.+?)/! or next;
    $query = unesc($1);
  } elsif ($host =~ /btjunkie/i) {
    $site = "btjunkie";
    $rest =~ /q=(.+?)(?:&|\z)/ or next;
    $query = unesc($1);
  # 2008-10-16
  } elsif ($host =~ /baidu/i) {
    $site = "baidu";
    $rest =~ /(?:wd|word)=(.+?)(?:&|\z)/ or next;
    $query = unesc($1);
  # 2012-04-06
  } elsif ($host =~ /youtube\.com/i) {
    $site = "youtube";
    $rest =~ /search_query=(.+?)(?:&|\z)/ or next;
    $query = unesc($1);
  # 2012-04-06
  } elsif ($host =~ /search\.cpan\.org/i) {
    $site = "searchcpan";
    $rest =~ /query=(.+?)(?:&|\z)/ or next;
    $query = unesc($1);
  # 2012-04-06
  } elsif ($host =~ /metacpan\.org/i) {
    $site = "metacpan";
    $rest =~ /q=(.+?)(?:&|\z)/ or next;
    $query = unesc($1);
  # 2012-04-06
  } elsif ($host =~ /duckduckgo\.com/i) {
    $site = "ddg";
    $rest =~ /q=(.+?)(?:&|\z)/ or next;
    $query = unesc($1);
  # 2012-11-29
  } elsif ($host =~ /amazon\.(com|ca|fr|co\.uk|cn|it|es|es|de)/i) {
    $site = "amazon";
    $rest =~ /field-keywords=(.+?)(?:&|\z)/ or next;
    $query = unesc($1);
  # 2014-04-09
  # 2015-01-13 - ternyata udah ganti domain jadi kickass.so
 } elsif ($host =~ /kickass\.[ts]o/i) {
    $site = "kat";
    $rest =~ m!/usearch/(.+)/! or next;
    $query = unesc($1);
  # 2015-03-02
  } elsif ($host =~ /bing\.com/i) {
    $rest =~ m!/(images|search|videos|news|maps)!i or next;
    $x = $1;
    $site =
        $x =~ /images/ ? "bing images" :
        $x =~ /videos/ ? "bing videos" :
        $x =~ /maps/ ? "bing maps" :
        $x =~ /news/ ? "bing news" :
        "bing";
    $rest =~ /q=(.+?)(?:&|\z)/ or next;
    $query = unesc($1);
  # 2017-11-01
  } elsif ($host =~ /tokopedia\.com/i) {
    $site = "tokopedia";
    $rest =~ m!q=(.+?)(?:&|\z)! or next;
    $query = unesc($1);
  } elsif ($host =~ /shopee\.co\.id/i) {
    $site = "shopee";
    $rest =~ m!keyword=(.+?)(?:&|\z)! or next;
    $query = unesc($1);
  } elsif ($host =~ /jd\.id/i) {
    $site = "jd.id";
    $rest =~ m!keywords=(.+?)(?:&|\z)! or next;
    $query = unesc($1);
  } elsif ($host =~ /lazada\.co\,id/i) {
    $site = "lazada";
    $rest =~ m!q=(.+?)(?:&|\z)! or next;
    $query = unesc($1);
  } elsif ($host =~ /blibli\.com/i) {
    $site = "blibli";
    $rest =~ m!/q-(.+?)! or next;
    $query = unesc($1);
    $query =~ s/-/ /g;
  }

  next unless $site;
  printf "%-15s %s\n", $site, uri_unescape($query);
}
