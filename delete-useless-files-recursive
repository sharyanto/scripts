#!/usr/bin/env perl

use 5.010;
use strict;
use warnings;
use experimental 'smartmatch';
use Log::ger::App;
BEGIN { $ENV{LOG_LEVEL} //= 'info' }
use Log::ger;

use File::Find;
use File::Path qw(remove_tree);

my @uselessn = qw(
                     desktop.ini
                     thumbs.db
                     .mediaartlocal
                     .directory
             );

my @args = @ARGV ? @ARGV : (".");

finddepth sub {
    log_trace("%s/%s", $File::Find::dir, $_);
    return if /^(\.\.?)$/;

    my $delete;
    {
        # exact filenames
        if (lc($_) ~~ @uselessn) {
            $delete++; last;
        }

        # filename pattern
        if (/(?:torrent downloaded|demonoid).*\.txt$/i) {
            $delete++; last;
        }

        # ext
        if (/\.(lnk|url|nfo|m3u|sfv)$/i) {
            $delete++; last;
        }
    }
    return unless $delete;

    log_info("Deleting %s/%s ...", $File::Find::dir, $_);
    remove_tree $_;
}, @args;
